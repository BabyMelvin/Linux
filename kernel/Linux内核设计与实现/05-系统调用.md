主要内容：

1. 什么是系统调用
2. Linux上的系统调用实现原理
3. 一个简单的系统调用的实现

# 1.什么是系统调用
简单来说，系统调用就是用户程序和硬件设备之间的桥梁。用户程序在需要的时候，通过系统调用来使用硬件设备。系统调用的存在，有以下重要的意义:


1. 用户程序通过系统调用来使用硬件，而不用关心具体的硬件设备，这样大大简化了用户程序的开发。比如：用户程序通过write()系统调用就可以将数据写入文件，而不必关心文件是在磁盘上还是软盘上，或者其他存储上。
2. 系统调用使得用户程序有更好的可移植性。只要操作系统提供的系统调用接口相同，用户程序就可在不用修改的情况下，从一个系统迁移到另一个操作系统。
3. 系统调用使得内核能更好的管理用户程序，增强了系统的稳定性。因为系统调用是内核实现的，内核通过系统调用来控制开放什么功能及什么权限给用户程序。这样可以避免用户程序不正确的使用硬件设备，从而破坏了其他程序。
4. 系统调用有效的分离了用户程序和内核的开发。用户程序只需关心系统调用API，通过这些API来开发自己的应用，不用关心API的具体实现。内核则只要关心系统调用API的实现，而不必管它们是被如何调用的。

# 2. Linux上的系统调用实现原理
要想实现系统调用，主要实现以下几个方面：

1. 通知内核调用一个哪个系统调用
2. 用户程序把系统调用的参数传递给内核
3. 用户程序获取内核返回的系统调用返回值

下面看看Linux是如何实现上面3个功能的。
## 2.1 通知内核调用一个哪个系统调用
每个系统调用都有一个系统调用号，系统调用发生时，内核就是根据传入的系统调用号来知道是哪个系统调用的。

在x86架构中，用户空间将系统调用号是放在`eax`中的，系统调用处理程序通过eax取得系统调用号。系统调用号定义在内核代码：`arch/alpha/include/asm/unistd.h`中，可以看出linux的系统调用不是很多。

## 2.2 用户程序把系统调用的参数传递给内核
系统调用的参数也是通过寄存器传给内核的，在x86系统上，系统调用的前5个参数放在ebx,ecx,edx,esi和edi中，如果参数多的话，还需要用个单独的寄存器存放指向所有参数在用户空间地址的指针。

一般的系统调用都是通过C库(最常用的是glibc库)来访问的，Linux内核提供一个从用户程序直接访问系统调用的方法。

参见内核代码：`arch/cris/include/arch-v10/arch/unistd.h`. arm中的路径：`arch/arm64/include/asm/unistd32.h`

里面定义了6个宏，分别可以调用参数个数为0～6的系统调用

```
_syscall0(type,name)
_syscall1(type,name,type1,arg1)
_syscall2(type,name,type1,arg1,type2,arg2)
_syscall3(type,name,type1,arg1,type2,arg2,type3,arg3)
_syscall4(type,name,type1,arg1,type2,arg2,type3,arg3,type4,arg4)
_syscall5(type,name,type1,arg1,type2,arg2,type3,arg3,type4,arg4,type5,arg5)
_syscall6(type,name,type1,arg1,type2,arg2,type3,arg3,type4,arg4,type5,arg5,type6,arg6)
```

超过6个参数的系统调用很罕见，所以这里只定义了6个。

## 2.3 用户程序获取内核返回的系统调用返回值
获取系统调用的返回值也是通过寄存器，在x86系统上，返回值放在eax中。

# 3.一个简单的系统调用的实现
了解了Linux上系统调用的原理，下面就可以自己来实现一个简单的系统调用。

## 3.1 环境准备
为了不破坏现有系统，我是用虚拟机来实验的。

主机：fedora16 x86_64系统 + kvm(一种虚拟技术，就像virtualbox，vmware等)

虚拟机: 也是安装fedora16 x86_64系统(通过virt-manager很容易安装一个系统)

## 3.2 修改内核源码中的相应文件
主要修改以下文件：

```
arch/x86/ia32/ia32entry.S
arch/x86/include/asm/unistd_32.h
arch/x86/include/asm/unistd_64.h
arch/x86/kernel/syscall_table_32.S
include/asm-generic/unistd.h
include/linux/syscalls.h
kernel/sys.c
```
我在sys.c中追加了2个函数:sys_foo和sys_bar

如果是在x86_64的内核中增加一个系统调用，只需修改 arch/x86/include/asm/unistd_64.h，比如sys_bar。

修改内容参见下面的diff文件：

```
diff -r new/arch/x86/ia32/ia32entry.S old/arch/x86/ia32/ia32entry.S
855d854
<     .quad sys_foo
diff -r new/arch/x86/include/asm/unistd_32.h old/arch/x86/include/asm/unistd_32.h
357d356
< #define __NR_foo    349
361c360
< #define NR_syscalls 350
---
> #define NR_syscalls 349
diff -r new/arch/x86/include/asm/unistd_64.h old/arch/x86/include/asm/unistd_64.h
689,692d688
< #define __NR_foo            312
< __SYSCALL(__NR_foo, sys_foo)
< #define __NR_bar            313
< __SYSCALL(__NR_bar, sys_bar)
diff -r new/arch/x86/kernel/syscall_table_32.S old/arch/x86/kernel/syscall_table_32.S
351d350
<     .long sys_foo
diff -r new/include/asm-generic/unistd.h old/include/asm-generic/unistd.h
694,695d693
< #define __NR_foo 272
< __SYSCALL(__NR_foo, sys_foo)
698c696
< #define __NR_syscalls 273
---
> #define __NR_syscalls 272
diff -r new/kernel/sys.c old/kernel/sys.c
1920,1928d1919
< 
< asmlinkage long sys_foo(void)
< {
<     return 1112223334444555;
< }
< asmlinkage long sys_bar(void)
< {
<     return 1234567890;
< }
```

## 3.3 编译内核

```
#cd linux-3.2.28
#make menuconfig  (选择要编译参数，如果不熟悉内核编译，用默认选项即可)
#make all  (这一步真的时间很长......)
#make modules_install
#make install  (这一步会把新的内核加到启动项中)
#reboot  (重启系统进入新的内核)
```

## 3.4 编写调用的系统调用的代码

```c
#include <unistd.h>
#include <sys/syscall.h>
#include <string.h>
#include <stdio.h>
#include <errno.h>


#define __NR_foo 312
#define __NR_bar 313

int main()
{
        printf ("result foo is %ld\n", syscall(__NR_foo));
        printf("%s\n", strerror(errno));
        printf ("result bar is %ld\n", syscall(__NR_bar));
        printf("%s\n", strerror(errno));
        return 0;
}
```

编译运行上面的代码：

```c
#gcc test.c -o test
#./test
```
运行结果如下：

```
result foo is 1112223334444555
Success
result bar is 1234567890
Success
```

# 4. Arm体系结构
 Unix系统通过向内核发出系统调用（system call）实现了用户态进程和硬件设备之间的大部分接口。系统调用是操作系统提供的服务，用户程序通过各种系统调用，来引用内核提供的各种服务，系统调用的执行让用户程序陷入内核，该陷入动作由`swi软中断`完成。

 应用编程接口（API）与系统调用的不同在于，前者只是一个函数定义，说明了如何获得一个给定的服务，而后者是通过软件中断向内核发出的一个明确的请求。

 当用户态的进程调用一个系统调用时，**CPU**切换到内核态并开始执行一个内核函数。因为内核实现了很多不同的系统调用，因此进程必须传递一个名为系统调用号（system call number）的参数来识别所需的系统调用。

## 4.1 SWI
ARM Linux 系统利用SWI指令来从用户空间进入内核空间，还是先让我们了解下这个SWI指令吧。

SWI指令用于**产生软件中断**，从而实现从用户模式变换到管理模式，CPSR保存到管理模式的SPSR，执行转移到SWI向量。在其他模式下也可使用SWI指令，处理器同样地切换到管理模式。指令格式如下：
`SWI{cond} immed_24`

其中：immed_24:24位立即数，值为从0――16777215之间的整数。

使用SWI指令时，通常使用以下下两种方法进行参数传递，SWI异常处理程序可以提供相关的服务，这两种方法均是**用户软件协定**。SWI异常中断处理程序要通过读取引起软件中断的SWI指令，以取得24为立即数。

*  1.指令中24位的立即数指定了用户请求的服务类型，参数通过通用寄存器传递。如：

```s
MOV R0,#34
SWI 12
```

* 2.指令中的24位立即数被忽略，用户请求的服务类型(12)由寄存器R0的值决定，参数(34)通过其他的通用寄存器传递。如：

```s
MOV R0, #12
MOV R1, #34
SWI 0
```
在SWI异常处理程序中，去除SWI立即数的步骤为：首先确定一起软中断的SWI指令时ARM指令还是Thumb指令，这可通过对SPSR访问得到；然后取得该SWI指令的地址，这可通过访问LR寄存器得到；接着读出指令，分解出立即数（低24位）。 

## 4.2 代码分析

下面的代码大家可以在entry-common.S中找到。在2.6.21中，认真研究大家会发现，你回避不了这样一个概念，EABI是什么东西？内核里面谈`EABI`,`OABI`,其实相对于系统调用的方式，当然我们所说的系统限于arm系统。 

`EABI (Extended ABI)`,说的是这样的一种新的系统调用方式

```s
mov r7, #num 
swi 0x0
```
 原来的系统调用方式是这样:`swi (#num | 0x900000)` (0x900000是个magic值)
也就是说原来的调用方式(Old ABI)是通过跟随在swi指令中的调用号来进行的，现在的是根据r7中的值。
现在看两个宏：

* CONFIG_OABI_COMPAT:意思是说和old ABI兼容
* CONFIG_AEABI:意思是说指定现在的方式为EABI

这两个宏可以同时配置，也可以都不配，也可以配置任何一种。我说一下内核是怎么处理这一问题的。 我们知道，`sys_call_table`在内核中是个跳转表，这个表中存储的是一系列的函数指针，这些指针就是系统调用函数的指针，如(sys_open).系统调用是根据一个调用号（通常就是表的索引）找到实际该调用内核哪个函数，然后运行该函数完成的。

首先，对于old ABI,内核给出的处理是给它建立一个单独的system call table,叫`sys_oabi_call_table`，这样，兼容方式下就会有两个system call table, 以old ABI方式的系统调用会执行old_syscall_table表中的系统调用函数，EABI方式的系统调用会用sys_call_table中的函数指针。

配置无外乎以下4中

* 第一:两个宏都配置 行为就是上面说的那样
* 第二:只配置CONFIG_OABI_COMPAT ， 那么以old ABI方式调用的会用`sys_oabi_call_table`，以EABI方式调用的 用`sys_call_table`，和1实质相同，只是情况1更加明确。
* 第三:只配置CONFIG_AEABI 系统中不存在 `sys_oabi_call_table`， 对old ABI方式调用不兼容。只能 以EABI方式调用，用sys_call_table
* 第四:两个都没有配置 系统默认会只允许old ABI方式，但是不存在old_syscall_table，最终会通过sys_call_table 完成函数调用

可以参考下面的代码，对我们的项目比较有用。

```s
.align 5   
ENTRY(vector_swi)   
sub sp, sp, #S_FRAME_SIZE   
stmia sp, {r0 - r12} @ Calling r0 - r12   
add r8, sp, #S_PC   
stmdb r8, {sp, lr}^ @ Calling sp, lr   
mrs r8, spsr @ called from non-FIQ mode, so ok.   
str lr, [sp, #S_PC] @ Save calling PC   
str r8, [sp, #S_PSR] @ Save CPSR   
str r0, [sp, #S_OLD_R0] @ Save OLD_R0   
zero_fp   
  
/*   
* Get the system call number.   
*/   
  
#if defined(CONFIG_OABI_COMPAT)   
  
/*   
* If we have CONFIG_OABI_COMPAT then we need to look at the swi   
* value to determine if it is an EABI or an old ABI call.   
*/   
#ifdef CONFIG_ARM_THUMB   
tst r8, #PSR_T_BIT   
movne r10, #0 @ no thumb OABI emulation   
ldreq r10, [lr, #-4] @ get SWI instruction   
#else   
ldr r10, [lr, #-4] @ get SWI instruction   
A710( and ip, r10, #0x0f000000 @ check for SWI )   
A710( teq ip, #0x0f000000 )   
A710( bne .Larm710bug )   
#endif   
  
#elif defined(CONFIG_AEABI)   
  
/*   
* Pure EABI user space always put syscall number into scno (r7).   
*/   
A710( ldr ip, [lr, #-4] @ get SWI instruction )   
A710( and ip, ip, #0x0f000000 @ check for SWI )   
A710( teq ip, #0x0f000000 )   
A710( bne .Larm710bug )   
  
#elif defined(CONFIG_ARM_THUMB)   
  
/* Legacy ABI only, possibly thumb mode. */   
tst r8, #PSR_T_BIT @ this is SPSR from save_user_regs   
addne scno, r7, #__NR_SYSCALL_BASE @ put OS number in   
ldreq scno, [lr, #-4]   
  
#else   
  
/* Legacy ABI only. */   
ldr scno, [lr, #-4] @ get SWI instruction   
A710( and ip, scno, #0x0f000000 @ check for SWI )   
A710( teq ip, #0x0f000000 )   
A710( bne .Larm710bug )   
  
#endif   
  
#ifdef CONFIG_ALIGNMENT_TRAP   
ldr ip, __cr_alignment   
ldr ip, [ip]   
mcr p15, 0, ip, c1, c0 @ update control register   
#endif   
enable_irq   
  
get_thread_info tsk   
adr tbl, sys_call_table @ load syscall table pointer   
ldr ip, [tsk, #TI_FLAGS] @ check for syscall tracing   
  
#if defined(CONFIG_OABI_COMPAT)   
/*   
* If the swi argument is zero, this is an EABI call and we do nothing.   
*   
* If this is an old ABI call, get the syscall number into scno and   
* get the old ABI syscall table address.   
*/   
bics r10, r10, #0xff000000   
eorne scno, r10, #__NR_OABI_SYSCALL_BASE   
ldrne tbl, =sys_oabi_call_table   
#elif !defined(CONFIG_AEABI)   
bic scno, scno, #0xff000000 @ mask off SWI op-code   
eor scno, scno, #__NR_SYSCALL_BASE @ check OS number   
#endif   
  
stmdb sp!, {r4, r5} @ push fifth and sixth args   
tst ip, #_TIF_SYSCALL_TRACE @ are we tracing syscalls?   
bne __sys_trace   
  
cmp scno, #NR_syscalls @ check upper syscall limit   
adr lr, ret_fast_syscall @ return address   
ldrcc pc, [tbl, scno, lsl #2] @ call sys_* routine   
  
add r1, sp, #S_OFF   
2: mov why, #0 @ no longer a real syscall   
cmp scno, #(__ARM_NR_BASE - __NR_SYSCALL_BASE)   
eor r0, scno, #__NR_SYSCALL_BASE @ put OS number back   
bcs arm_syscall   
b sys_ni_syscall @ not private func   
  
/*   
* This is the really slow path. We're going to be doing   
* context switches, and waiting for our parent to respond.   
*/   
__sys_trace:   
mov r2, scno   
add r1, sp, #S_OFF   
mov r0, #0 @ trace entry [IP = 0]   
bl syscall_trace   
  
adr lr, __sys_trace_return @ return address   
mov scno, r0 @ syscall number (possibly new)   
add r1, sp, #S_R0 + S_OFF @ pointer to regs   
cmp scno, #NR_syscalls @ check upper syscall limit   
ldmccia r1, {r0 - r3} @ have to reload r0 - r3   
ldrcc pc, [tbl, scno, lsl #2] @ call sys_* routine
```
系统调用是os操作系统提供的服务,用户程序通过各种系统调用,来引用内核提供的各种服务,系统调用的执行让用户程序陷入内核,该陷入动作由swi软中断完成.
At91rm9200处理器对应的linux2.4.19内核系统调用对应的软中断定义如下:

```s
#if defined(__thumb__)                             //thumb模式  
#define __syscall(name)                          \  
    "push    {r7}\n\t"                           \  
    "mov    r7, #" __sys1(__NR_##name) "\n\t"    \  
    "swi    0\n\t"                               \  
    "pop    {r7}"  
#else                                              //arm模式  
#define __syscall(name) "swi\t" __sys1(__NR_##name) "\n\t"  
#endif  
  
#define __sys2(x) #x  
#define __sys1(x) __sys2(x)  
#define __NR_SYSCALL_BASE    0x900000               //此为OS_NUMBER << 20运算值  
#define __NR_open            (__NR_SYSCALL_BASE+ 5) //0x900005 
```

举一个例子来说:open系统调用,**库函数**最终会调用`__syscall(open)`,宏展开之后为`swi #__NR_open`,即,`swi #0x900005`触发中断,中断号0x900005存放在`[lr,#-4]`地址中,处理器跳转到`arch/arm/kernel/entry-common.S`中vector_swi读取`[lr,#-4]`地址中的中断号,之后查询`arch/arm/kernel/entry-common.S`中的`sys_call_table`系统调用表,该表内容在`arch/arm/kernel/calls.S`中定义,`__NR_open`在表中对应的顺序号为__syscall_start:

```s
.long    SYMBOL_NAME(sys_open)                     //第5个  
...  
将sys_call_table[5]中内容传给pc,系统进入sys_open函数,处理实质的open动作  
  
注:用到的一些函数数据所在文件,如下所示  
arch/arm/kernel/calls.S声明了系统调用函数  
include/asm-arm/unistd.h定义了系统调用的调用号规则  
  
vector_swi定义在arch/arm/kernel/entry-common.S  
vector_IRQ定义在arch/arm/kernel/entry-armv.S  
vector_FIQ定义在arch/arm/kernel/entry-armv.S  
  
arch/arm/kernel/entry-common.S中对sys_call_table进行了定义:  
    .type    sys_call_table, #object  
ENTRY(sys_call_table)  
#include "calls.S"                                 //将calls.S中的内容顺序链接到这里  
```

源程序：

```s
ENTRY(vector_swi)  
    save_user_regs  
    zero_fp  
    get_scno                                        //将[lr,#-4]中的中断号转储到scno(r7)  
    arm710_bug_check scno, ip  
#ifdef CONFIG_ALIGNMENT_TRAP  
    ldr    ip, __cr_alignment  
    ldr    ip, [ip]  
    mcr    p15, 0, ip, c1, c0                       @ update control register  
#endif  
    enable_irq ip  
  
    str    r4, [sp, #-S_OFF]!                       @ push fifth arg  
  
    get_current_task tsk  
    ldr    ip, [tsk, #TSK_PTRACE]                   @ check for syscall tracing  
    bic    scno, scno, #0xff000000                  @ mask off SWI op-code  
//#define OS_NUMBER    9[entry-header.S]  
//所以对于上面示例中open系统调用号scno=0x900005  
//eor scno,scno,#0x900000  
//之后scno=0x05  
    eor    scno, scno, #OS_NUMBER << 20             @ check OS number  
//sys_call_table项为calls.S的内容  
    adr    tbl, sys_call_table                      @ load syscall table pointer  
    tst    ip, #PT_TRACESYS                         @ are we tracing syscalls?  
    bne    __sys_trace  
  
    adrsvc    al, lr, ret_fast_syscall              @ return address  
    cmp    scno, #NR_syscalls                       @ check upper syscall limit  
//执行sys_open函数  
    ldrcc    pc, [tbl, scno, lsl #2]                @ call sys_* routine  
    add    r1, sp, #S_OFF  
2:  mov    why, #0                                  @ no longer a real syscall  
    cmp    scno, #ARMSWI_OFFSET  
    eor    r0, scno, #OS_NUMBER << 20               @ put OS number back  
    bcs    SYMBOL_NAME(arm_syscall)      
    b    SYMBOL_NAME(sys_ni_syscall)                @ not private func  
    /*  
     * This is the really slow path. We're going to be doing  
     * context switches, and waiting for our parent to respond.  
     */  
__sys_trace:  
    add    r1, sp, #S_OFF  
    mov    r0, #0                                   @ trace entry [IP = 0]  
    bl    SYMBOL_NAME(syscall_trace)  
/*  
//2007-07-01 gliethttp [entry-header.S]  
//Like adr, but force SVC mode (if required)  
  .macro adrsvc, cond, reg, label  
     adr\cond \reg, \label  
  .endm  
//对应反汇编:  
//add lr, pc, #16 ; lr = __sys_trace_return  
*/  
    adrsvc    al, lr, __sys_trace_return            @ return address  
    add    r1, sp, #S_R0 + S_OFF                    @ pointer to regs  
    cmp    scno, #NR_syscalls                       @ check upper syscall limit  
    ldmccia    r1, {r0 - r3}                        @ have to reload r0 - r3  
    ldrcc    pc, [tbl, scno, lsl #2]                @ call sys_* routine  
    b    2b  
  
__sys_trace_return:  
    str    r0, [sp, #S_R0 + S_OFF]!                 @ save returned r0  
    mov    r1, sp  
    mov    r0, #1                                   @ trace exit [IP = 1]  
    bl    SYMBOL_NAME(syscall_trace)  
    b    ret_disable_irq  
  
    .align    5  
#ifdef CONFIG_ALIGNMENT_TRAP  
    .type    __cr_alignment, #object  
__cr_alignment:  
    .word    SYMBOL_NAME(cr_alignment)  
#endif  
  
    .type    sys_call_table, #object  
ENTRY(sys_call_table)  
#include "calls.S"  .
```