接上节, 我们先来看看`bl preserve_boot_args`


```asm
/*
 * Preserve the arguments passed by the bootloader in x0 .. x3
 */
preserve_boot_args:
    mov x21, x0             // x21=FDT

    adr_l   x0, boot_args           // record the contents of
    stp x21, x1, [x0]           // x0 .. x3 at kernel entry
    stp x2, x3, [x0, #16]

    dmb sy              // needed before dc ivac with
                        // MMU off

    mov x1, #0x20           // 4 x 8 bytes
    b   __inval_dcache_area     // tail call
ENDPROC(preserve_boot_args)
```
代码的含义一目了然, 把存fdt内存地址的x0保存到x21寄存器. 然后把启动参数x0, x1, x2, x3全部保存到boot_args数组中.

arm64 linux规定:

Primary CPU general-purpose register settings

* x0 = physical address of device tree blob (dtb) in system RAM.
* x1 = 0 (reserved for future use)
* x2 = 0 (reserved for future use)
* x3 = 0 (reserved for future use)


这里值得注意的有几点

# 1.这里有用到adr_l

arm64并没有这个指令, 这是一个宏

```asm
/*
* Pseudo-ops for PC-relative adr/ldr/str <reg>, <symbol> where
* <symbol> is within the range +/- 4 GB of the PC when running
* in core kernel context. In module context, a movz/movk sequence
* is used, since modules may be loaded far away from the kernel
* when KASLR is in effect.
*/
      /*
       * @dst: destination register (64 bit wide)
       * @sym: name of the symbol
       */
      .macro  adr_l, dst, sym
  #ifndef MODULE
      adrp    \dst, \sym    
      add \dst, \dst, :lo12:\sym
  #else
      movz    \dst, #:abs_g3:\sym
      movk    \dst, #:abs_g2_nc:\sym
      movk    \dst, #:abs_g1_nc:\sym
      movk    \dst, #:abs_g0_nc:\sym
  #endif                
      .endm

```
* ADR指令:小范围的地址读取指令,它将基于PC的相对偏移的地址读到目标寄存器中；格式:`ADR register exper`

	* 编译时，首先会计算出当前PC到exper的偏移量`#offset_to_exper`
	* 然后会用ADD或SUB指令，来替换这条指令；例如`ADD register,PC,#offset_to_exper`
	* register就是exper的地址
* ADRP指令
	* 编译时，首先会计算出当前PC到exper的偏移量`#offset_to_exper`
	* pc的低12位清零，然后加上偏移量，给register
	* 得到的地址，是含有label的4KB对齐内存区域的base地址；

可以看到, 这里的adr_l拆分成了两条指令, adrp + add, adrp指令最大寻址空间时+-4GB, 但是所寻址的地址是4KB对齐的. 所以这里在加了一个add指令来修正地址的低12bit, 从而实现了这个加载`+-4GB`任意位置的运行时地址的宏.

其中boot_args[4]； 定义在`arch/arm64/kernel/setup.c:u64 __cacheline_aligned boot_args[4];`


# 2.

`__inval_dcache_area`函数用来invalidate指定区域的dcache, 具体如下

```asm

/*
 *  __inval_dcache_area(kaddr, size)
 *
 *  Ensure that any D-cache lines for the interval [kaddr, kaddr+size)
 *  are invalidated. Any partial lines at the ends of the interval are
 *  also cleaned to PoC to prevent data loss.
 *
 *  - kaddr   - kernel address
 *  - size    - size in question
 */
ENTRY(__inval_dcache_area)
    /* FALLTHROUGH */

/*
 *  __dma_inv_area(start, size)
 *  - start   - virtual start address of region
 *  - size    - size in question
 */ 
__dma_inv_area:
    add x1, x1, x0
    dcache_line_size x2, x3
    sub x3, x2, #1
    tst x1, x3              // end cache line aligned?
    bic x1, x1, x3
    b.eq    1f
    dc  civac, x1           // clean & invalidate D / U line
1:  tst x0, x3              // start cache line aligned?
    bic x0, x0, x3
    b.eq    2f
    dc  civac, x0           // clean & invalidate D / U line
    b   3f
2:  dc  ivac, x0            // invalidate D / U line
3:  add x0, x0, x2
    cmp x0, x1
    b.lo    2b
    dsb sy
    ret
ENDPIPROC(__inval_dcache_area)
ENDPROC(__dma_inv_area)
```
可以看到如果指定内存区域有跨越cacheline, 那么对两边跨越了cacheline的地址使用的clean + invalidate, 对于中间区域可以直接invalidate不用写回内存, 从而加快invalidate速度.