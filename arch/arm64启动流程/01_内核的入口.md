# arm64_linux启动流程分析01_内核的入口

首先看链接文件如下

```
ENTRY(_text)
SECTIONS
{
	.....省略部分无关内容....

	. = KIMAGE_VADDR + TEXT_OFFSET;

	.head.text : {
		_text = .;
		HEAD_TEXT
	}
```

可以看到入口点在`_text`. 而`_text`是指向`.head.text`段的起始位置. 所以内核是从`.head.text`开始运行的. 那么这个段是写什么内容呢?

```
#define HEAD_TEXT *(.head.text)
```
在`arch/arm64/kernel/head.S`中有

```arm
	__HEAD
_head:
	/*
	 * DO NOT MODIFY. Image header expected by Linux boot-loaders.
	 */
#ifdef CONFIG_EFI
	/*
	 * This add instruction has no meaningful effect except that
	 * its opcode forms the magic "MZ" signature required by UEFI.
	 */
	add	x13, x18, #0x16
	b	stext
#else
	b	stext				// branch to kernel start, magic
	.long	0				// reserved
#endif
	le64sym _kernel_offset_le       // Image load offset from start of RAM, little-endian
    le64sym _kernel_size_le         // Effective size of kernel image, little-endian
    le64sym _kernel_flags_le        // Informative flags, little-endian
    .quad   0               // reserved
    .quad   0               // reserved
    .quad   0               // reserved
    .ascii  "ARM\x64"           // Magic number
#ifdef CONFIG_EFI
    .long   pe_header - _head       // Offset to the PE header.

pe_header:
    __EFI_PE_HEADER
#else
    .long   0               // reserved
#endif
```

* 放置的内容指令(32)则，两条指令之后，存放了，arm64 linux header

所以对应到code, 内核是从_head开始运行的. 这个开始点放置了一个arm64 linux的header:(`__HEAD`:`#define __HEAD      .section    ".head.text","ax"`)


其中：

```
/*
 * Emit a 64-bit absolute little endian symbol reference in a way that
 * ensures that it will be resolved at build time, even when building a
 * PIE binary. This requires cooperation from the linker script, which
 * must emit the lo32/hi32 halves individually.
*/
.macro  le64sym, sym
    .long   \sym\()_lo32
	.long   \sym\()_hi32
.endm
```

64bit小端格式符号，在编译阶段确定。链接脚本确保低32位和高32位独立。要来编译阶段确定是小端格式的64位数

```
//image.h中定义了 _kernel_offset_le
/*
* These will output as part of the Image header, which should be little-endian
* regardless of the endianness of the kernel. While constant values could be
* endian swapped in head.S, all are done here for consistency.
*/
#define HEAD_SYMBOLS                        \
      DEFINE_IMAGE_LE64(_kernel_size_le, _end - _text);   \
      DEFINE_IMAGE_LE64(_kernel_offset_le, TEXT_OFFSET);  \
      DEFINE_IMAGE_LE64(_kernel_flags_le, __HEAD_FLAGS);

#define DEFINE_IMAGE_LE64(sym, data)                \
      sym##_lo32 = DATA_LE32((data) & 0xffffffff);        \
      sym##_hi32 = DATA_LE32((data) >> 32)

#ifdef CONFIG_CPU_BIG_ENDIAN
  #define DATA_LE32(data)             \
      ((((data) & 0x000000ff) << 24) |    \
       (((data) & 0x0000ff00) << 8)  |    \
       (((data) & 0x00ff0000) >> 8)  |    \
       (((data) & 0xff000000) >> 24))
#else
  #define DATA_LE32(data) ((data) & 0xffffffff)
#endif
```

* 注意HEAD_SYMBOLS是在vmlinux.lds.S被调用的
宏展开内容为：

```
_kernel_size_le_lo32 = DATA_LE32(_end - _text) & 0xffffffff;
_kernel_size_le_hi32 = DATA_LE32((data) >> 32
```
将lds中展开的宏，就可以得到le64sym宏的内容了

* size：_end - _text
* offset:TEXT_OFFSET值为(Makefile中指定,`TEXT_OFFSET := 0x00080000`)
	* 传递到lds方法：`CPPFLAGS_vmlinux.lds	:= -DTEXT_OFFSET=$(TEXT_OFFSET)`
* flags:`__HEAD_FLAGS`

注意： `make archhelp`可以单独编译dts

将上面转换为c，来看的ARM开始存放的内容为：

```c
u32 code0;			/* Executable code */
u32 code1;			/* Executable code */
u64 text_offset;		/* Image load offset, little endian */
u64 image_size;		/* Effective Image size, little endian */
u64 flags;			/* kernel flags, little endian */
u64 res2	= 0;		/* reserved */
u64 res3	= 0;		/* reserved */
u64 res4	= 0;		/* reserved */
u32 magic	= 0x644d5241;	/* Magic number, little endian, "ARM\x64" */
u32 res5;			/* reserved (used for PE COFF offset) */
```
这个头的前两个位置放置的是可执行code

# 2
在开启UEFI支持时, `add x13, x18, #0x16`这个code实际上是为了满足EFI格式的”MZ”头. 如果使用UEFI来启动kernel, 会识别出来并走UEFI启动的流程, 如果是普通的启动过程如使用uboot的booti进行引导, 那么第一条指令就是一条dummy指令. 第二条就跳转到`stext`运行了.

```
/*
 * The following callee saved general purpose registers are used on the
 * primary lowlevel boot path:
 *
 *  Register   Scope                      Purpose
 *  x21        stext() .. start_kernel()  FDT pointer passed at boot in x0
 *  x23        stext() .. start_kernel()  physical misalignment/KASLR offset
 *  x28        __create_page_tables()     callee preserved temp register
 *  x19/x20    __primary_switch()         callee preserved temp registers
 */
ENTRY(stext)
    bl  preserve_boot_args
    bl  el2_setup           // Drop to EL1, w0=cpu_boot_mode
    adrp    x23, __PHYS_OFFSET
    and x23, x23, MIN_KIMG_ALIGN - 1    // KASLR offset, defaults to 0
    bl  set_cpu_boot_mode_flag
    bl  __create_page_tables
    /*
     * The following calls CPU setup code, see arch/arm64/mm/proc.S for
     * details.
     * On return, the CPU will be ready for the MMU to be turned on and
     * the TCR will have been set.
     */
    bl  __cpu_setup         // initialise processor
    b   __primary_switch
ENDPROC(stext)
```
这段代码就是内核启动是运行的初始化代码. 后面会分章节来详细描述.